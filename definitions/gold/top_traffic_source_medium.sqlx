config {
  type: "table", 
  schema: "ga4_ecommerce", 
  name: "top_traffic_source_medium",
  tags: ["gold"],
  description: "Monthly aggregated report analyzing top traffic sources by purchase value and volume."
}

SELECT
  FORMAT_DATE('%Y-%m', event_date) AS month, /* Monthly aggregation */
  traffic_source_medium,
  SUM(event_value_in_usd) AS total_purchased_value_usd,
  COUNT(DISTINCT CONCAT(user_pseudo_id, ga_session_id, event_date)) AS total_purchases, /* Count of unique purchases */
  SUM(items_quantity) AS total_items_purchased, 
  SAFE_DIVIDE(SUM(items_quantity), COUNT(DISTINCT CONCAT(user_pseudo_id, ga_session_id, event_date))) AS avg_items_per_purchase /* Average items per purchase */
FROM
  ${ref("purchase_traffic_source_medium")}
GROUP BY
  month, traffic_source_medium
ORDER BY
  month DESC, total_purchased_value_usd DESC